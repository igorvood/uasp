package ru.vtb.uasp.validate;

global org.slf4j.Logger logger

import scala.Some
import ru.vtb.uasp.common.dto.UaspDto
import ru.vtb.uasp.validate.entity.ValidateMsg
import ru.vtb.uasp.mutator.configuration.drools.DrlHelper
import ru.vtb.bevent.first.salary.aggregate.Util.DrlCaseHelper
import java.util.Arrays

import ru.vtb.uasp.mutator.service.dto.*

dialect "mvel"


rule "is salary"
salience 9
when

    UaspDto($isIncome:dataBoolean.get("is_income"), DrlHelper.isNonEmpty($isIncome) && $isIncome.get == true)
    and
    UaspDto($dataKBO:dataString.get("kbo"), DrlHelper.isNonEmpty($dataKBO) && Arrays.asList("731500-00", "731500-01",  "731500-03").contains($dataKBO.get))
then
    //для продолжения определения первой зп 56 case
    insert(new UaspOperation(drools.getRule().getName(), new BooleanMap(true), "is_salary", new Add()))

    //для обновления состояния 44 case
    insert(new UaspOperation(drools.getRule().getName(), new BooleanMap(true), "system_is_update_44_case", new Add()))

end

rule "is pension"
salience 7
when

    UaspDto($isIncome:dataBoolean.get("is_income"), DrlHelper.isNonEmpty($isIncome) && $isIncome.get == true)
    and
    UaspDto($dataKBO:dataString.get("kbo"), DrlHelper.isNonEmpty($dataKBO) && Arrays.asList("731500-02",  "731500-04").contains($dataKBO.get))
    and
    UaspDto($pensionFlg:dataString.get("pension_flg"), DrlHelper.isNonEmpty($pensionFlg) && $pensionFlg.get == "Y")
    and
    (
    UaspDto($tranComment:dataString.get("tcmt"), DrlHelper.isNonEmpty($tranComment) && DrlCaseHelper.isCase57($tranComment))
    or
    UaspDto($dataKBOCft:dataString.get("kbo_cft"), DrlHelper.isNonEmpty($dataKBOCft))
    or
    UaspDto($dataKBOw4:dataString.get("kbo_w4"), DrlHelper.isNonEmpty($dataKBOw4))
    )
then

    insert(new UaspOperation(drools.getRule().getName(), new BooleanMap(true), "is_pension", new Add()))

end
